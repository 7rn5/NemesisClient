plugins {
    id 'java'
	  id 'fabric-loom' version '1.11-SNAPSHOT'
	  id 'maven-publish'
	  id 'com.github.gmazzo.buildconfig' version '5.5.1'
}

version = project.mod_version
group = project.maven_group

base {
	  archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
}

loom {
	  mods {
		    "nemesis" {
			      sourceSet sourceSets.main
		    }
	  }
}

dependencies {
	  minecraft "com.mojang:minecraft:${project.minecraft_version}"
	  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

def getHash() {
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

buildConfig {
    className("BuildConfig")
    packageName(group)
    buildConfigField(String, "CLIENT_VERSION", project.mod_version)
    buildConfigField(String, "GIT_HASH", "${getHash()}")
}

processResources {
	  inputs.property "version", project.version
	  filesMatching("fabric.mod.json") {
		    expand "version": inputs.properties.version
	  }
}

tasks.withType(JavaCompile).configureEach {
	  it.options.release = 21
}

java {
	  withSourcesJar()
	  sourceCompatibility = JavaVersion.VERSION_21
	  targetCompatibility = JavaVersion.VERSION_21
}

jar {
	  inputs.property "archivesName", project.base.archivesName
	  from("LICENSE") {
		    rename { "${it}_${inputs.properties.archivesName}"}
	  }
}

publishing {
	  publications {
		    create("mavenJava", MavenPublication) {
			      artifactId = project.archives_base_name
			      from components.java
		    }
	  }
	  repositories {
	  }
}

task updateReadme(type: Exec) {
    def script = "${projectDir}/readme_manager.py"
    commandLine 'python', script
}

tasks.register("GitCommit") {
    group = "automation"
    description = "Runs build, and if successful, commits and pushes to Git."
    
    dependsOn(updateReadme)
    
    doLast {
        println("âœ… Build finished, checking for changes...")
        
        def changes = new ByteArrayOutputStream()
        exec {
            commandLine "git", "status", "--porcelain"
            standardOutput = changes
        }
        
        if (changes.toString().trim().isEmpty()) {
            println "No changes detexted -- skipping commit & push"
            return
        }
        
        println("ðŸ“¦ Detected changes, committing...")
        
        exec {
            commandLine "git", "add", "."
        }
        
        exec {
            commandLine "git", "commit", "-m", "Auto build update on ${new Date().format('yyyy-MM-dd HH:mm:ss')}"
        }
        
        println "Pushing to Github..."
        exec {
            commandLine "git", "push"
        }
        
        println "âœ…  Git push  complete!"
    }
}

build.dependsOn(updateReadme)